plugins {
    id 'application'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'io.gitlab.arturbosch.detekt' version '1.21.0'
}

group 'br.com.correios.enderecador'
version '2.4.4'
mainClassName = 'br.com.correios.enderecador.ControleEnderecadorKt'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'https://repository.openmindonline.it/'
    }
    maven {
        url 'http://jasperreports.sourceforge.net/maven2/'
        allowInsecureProtocol = true
    }
    maven {
        url 'https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/'
        allowInsecureProtocol = true
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // Dependency injection
    ksp group: 'io.insert-koin', name: 'koin-ksp-compiler', version: '1.0.1'
    implementation group: 'io.insert-koin', name: 'koin-core', version: '3.2.0'
    implementation group: 'io.insert-koin', name: 'koin-annotations', version: '1.0.1'
    testImplementation group: 'io.insert-koin', name: 'koin-test', version: '3.2.0'

    // Database
    implementation group: 'com.couchbase.lite', name: 'couchbase-lite-java', version: '3.0.2'

    // UI
    implementation group: 'com.formdev', name: 'flatlaf', version: '2.4'
    implementation group: 'com.formdev', name: 'flatlaf-extras', version: '2.4'
    implementation group: 'com.formdev', name: 'flatlaf-intellij-themes', version: '2.4'
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '11.0'

    // Barcode
    implementation(group: 'com.google.zxing', name: 'javase', version: '3.5.0') {
        exclude(group: 'com.beust', module: 'jcommander')
    }

    // HTTP Library
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'

    // CLI
    implementation group: 'com.github.ajalt.clikt', name: 'clikt', version: '3.5.0'

    // Coroutine
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-swing', version: '1.6.4'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-reactive', version: '1.6.4'

    // Reports
    implementation group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.20.0'

    // Validation
    implementation group: 'io.konform', name: 'konform-jvm', version: '0.4.0'

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-reload4j', version: '2.0.2'

    // Serialization
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.13.3'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0"

    // Old
    implementation group: 'hsqldb', name: 'hsqldb', version: '1.8.0.1'
    implementation group: 'javax.help', name: 'javahelp', version: '2.0.05'
}

test {
    useJUnitPlatform()
}

kotlin {
    sourceSets {
        main.kotlin.srcDirs += 'build/generated/ksp/main/kotlin'
        test.kotlin.srcDirs += 'build/generated/ksp/test/kotlin'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}